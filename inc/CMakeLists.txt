# Use glob to find the headers
file(GLOB LAURA_HEADERS ${CMAKE_CURRENT_SOURCE_DIR}/*.hh)
if (NOT LAURA_BUILD_ROOFIT_TASK)
    list(REMOVE_ITEM LAURA_HEADERS ${CMAKE_CURRENT_SOURCE_DIR}/LauRooFitTask.hh)
endif()

# Generate the dictionary file
set(LAURA_LINKDEF ${CMAKE_CURRENT_SOURCE_DIR}/Laura++_LinkDef.h)
set(LAURA_DICTIONARY G__Laura++)
set_source_files_properties(${CMAKE_CURRENT_BINARY_DIR}/${LAURA_DICTIONARY}.cxx PROPERTIES COMPILE_FLAGS -Wno-shadow)
if (LAURA_BUILD_ROOFIT_TASK)
    ROOT_GENERATE_DICTIONARY(
        ${LAURA_DICTIONARY}
        ${LAURA_HEADERS}
        MODULE Laura++
        LINKDEF ${LAURA_LINKDEF}
        OPTIONS -DDOLAUROOFITTASK
        )
else()
    ROOT_GENERATE_DICTIONARY(
        ${LAURA_DICTIONARY}
        ${LAURA_HEADERS}
        MODULE Laura++
        LINKDEF ${LAURA_LINKDEF}
        )
endif()

# Install the pcm and rootmap files generated by ROOT_GENERATE_DICTIONARY
install(
    FILES ${CMAKE_CURRENT_BINARY_DIR}/libLaura++.rootmap ${CMAKE_CURRENT_BINARY_DIR}/libLaura++_rdict.pcm
    DESTINATION ${CMAKE_INSTALL_LIBDIR}
    )

# Install the headers
install(FILES ${LAURA_HEADERS} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${CMAKE_PROJECT_NAME})

